from "generics/interfaces.ato" import Power
from "generics/regulators.ato" import LDO, Buck
from "lv2842xlvddcr/lv2842kit.ato" import LV2842Kit
from "generics/diodes.ato" import Diode
from "ldk220m-r/elec/src/ldk220m-r.ato" import LDK220M_R
from "generics/diodes.ato" import SchottkyDiode
from "generics/regulators.ato" import AdjustableLDO

module PowerSupply:
    power_batt = new Power
    power_usb = new Power
    power_gate = new Power
    power5v = new Power
    power3v3 = new Power

    # Components
    buck = new Buck
    ldo5V = new AdjustableLDO
    ldo3V3 = new AdjustableLDO
    diode = new PowerDiode

    # Select componentsb
    buck -> LV2842Kit
    ldo5V -> LDK220M_R
    ldo3V3 -> LDK220M_R
    diode.diode -> Diode_1N5819WS

    # Configure components
    buck.v_in = 12V to 24V
    buck.v_out = 8V +/- 10%
    ldo5V.v_in = 8V +/- 10%
    ldo5V.v_out = 5V +/- 5%
    ldo3V3.v_in = 5V +/- 5%
    ldo3V3.v_out = 3.3V +/- 5%


    # Buck converter
    power_batt ~ buck.power_in
    power_gate ~ buck.power_out


    # Diode OR (USB or battery)
    power_usb ~ diode.power_in
    diode.power_out ~ power5v


    # LDOs
    power_gate ~ ldo5V.power_in
    power5v ~ ldo5V.power_out
    power5v ~ ldo3V3.power_in
    power3v3 ~ ldo3V3.power_out


module PowerDiode:
    """
    Wraps diode in with power interfaces
    """
    power_in = new Power
    power_out = new Power

    diode = new Diode

    power_in.vcc ~ diode.anode; diode.cathode ~ power_out.vcc



component Diode_1N5819WS from SchottkyDiode:
    # component 1N5819WS
    footprint = "SOD-323_L1.8-W1.3-LS2.5-RD"
    lcsc_id = "C191023"
    mpn = "C191023"
    # pins
    cathode ~ pin 1
    anode ~ pin 2
