from "generics/interfaces.ato" import Power, CAN, I2C, Pair, USB2
from "xt-connectors/xt-connectors.ato" import XT30_Male_Right_Angle
from "usb-connectors/usb-connectors.ato" import USBCConn
from "qwiic-connectors/elec/src/qwiic-connectors.ato" import QwiicConnector
from "jst-gh/elec/src/jst-gh.ato" import JSTGH4Pin
from "sk6805side/elec/src/sk6805side.ato" import SK6805SIDE
from "generics/buttons.ato" import ButtonPulldown, ButtonSKTDLDE010


module IOs:
    power_batt = new Power
    power_usb = new Power
    power5v = new Power
    power3v3 = new Power
    i2c = new I2C
    can = new CAN
    button = new Pair
    led = new Pair
    usb2 = new USB2
    out_a = new Pair
    out_b = new Pair
    out_c = new Pair

    # Connectors
    usbc_connector = new USBCConn
    xt30_connector_1 = new XT30_Male_Right_Angle
    xt30_connector_2 = new XT30_Male_Right_Angle
    i2c_connector = new QwiicConnector
    can_connector_1 = new JSTGH4Pin
    can_connector_2 = new JSTGH4Pin

    # Phase outputs
    pad_out_a = new PowerPad
    pad_out_b = new PowerPad
    pad_out_c = new PowerPad

    # LEDs
    led1 = new SK6805SIDE
    led2 = new SK6805SIDE

    # Button
    button_pulldown = new ButtonPulldown
    button_pulldown.btn.btn -> ButtonSKTDLDE010

    # Power connections
    power_batt ~ xt30_connector_1.power
    power_batt ~ xt30_connector_2.power
    power_usb ~ usbc_connector.power
    power5v ~ led1.power
    power5v ~ led2.power
    power5v ~ can_connector_1.power
    power5v ~ can_connector_2.power
    power3v3 ~ i2c_connector.power
    power3v3 ~ button_pulldown.power

    # Data connections
    usb2 ~ usbc_connector.usb2
    i2c ~ i2c_connector.i2c
    # can ~ can_connector_1.can
    # can ~ can_connector_2.can

    # LED connections
    led ~ led1.data_in
    led1.data_out ~ led2.data_in

    # Button connections
    button ~ button_pulldown.output

    # Output connections
    out_a ~ pad_out_a.output
    out_b ~ pad_out_b.output
    out_c ~ pad_out_c.output


component PowerPad:
    output = new Pair
    output.io ~ pin 1
    footprint = "lib:OutputPad"
    mpn = "OutputPad"
